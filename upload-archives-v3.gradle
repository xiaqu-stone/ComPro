apply plugin: 'maven'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

uploadArchives {
    repositories.mavenDeployer {
        def userName = properties.getProperty("REPO_USERNAME")
        def password = properties.getProperty("REPO_PASSWORD")
        def url
        if (ENABLE_SNAPSHOT) {
            url = REPO_URL_SNAPSHOT
            version = VERSION_SNAPSHOT
        } else {
            url = REPO_URL
            version = VERSION_NAME
        }
        repository(url: url) {//仓库地址
            //这里配置上传账户的用户名和密码
            authentication(userName: userName, password: password)
        }
        pom.groupId = GROUP_ID
        pom.artifactId = ARTIFACT_ID
        pom.version = version
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}